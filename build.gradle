plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.5.0'
    id 'checkstyle'
    id 'com.jfrog.artifactory'
}

group 'de.seitenbau.ozghub.prozesspipeline'

version = project.hasProperty('pluginVersion') ? pluginVersion : '99.0.0-SNAPSHOT'
println 'Plugin-Version ist  : ' + version

if (!project.hasProperty('artifactoryJenkinsPassword')
        && System.getenv('JENKINS_HOME') != null && !System.getenv('JENKINS_HOME').isEmpty()) {
    println 'WARN: Artifactory password is not set'
}

repositories {
    mavenLocal()
    maven {
        url 'http://maven2.seitenbau.net/artifactory/all'
        allowInsecureProtocol true
    }
}

dependencies {
    implementation gradleApi()
    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.16.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.16.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.0.1'
    implementation 'org.projectlombok:lombok:1.18.16'

    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.1.4'

    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'

    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    maxHeapSize = '1G'
    finalizedBy jacocoTestReport
    systemProperty 'file.encoding', 'UTF-8'
}

jacocoTestReport {
    dependsOn test
}

spotbugsMain {
    ignoreFailures = true
    effort = 'max'
    excludeFilter = file('src/main/resources/spotbugs-exclude.xml')
}

spotbugsTest.enabled = false

checkstyle {
    toolVersion = '8.8'
    checkstyleTest.enabled = false
}

// ##########  Lokaler Build  ##########
if (System.getenv('JENKINS_HOME') == null || System.getenv('JENKINS_HOME').isEmpty()) {
    publishing {
        repositories {
            maven {
                url mavenLocal()
            }
        }
    }
}

// ##########  Build auf Jenkins  ##########
else {
    apply plugin: 'com.jfrog.artifactory'

    def repositoryKey = project.hasProperty('repositoryKey') ? repositoryKey : 'ci-snapshots'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    artifactory {
        contextUrl = 'http://shadow-artifactory.infra.dev.seitenbau.net/artifactory'
        publish {
            repository {
                repoKey = repositoryKey
                username = 'jenkins'
                password = artifactoryJenkinsPassword
                maven = true
            }
            defaults {
                publications('mavenJava')
            }
        }
    }

    jar {
        baseName 'ozghub-prozess-pipeline-gradle-plugin'
    }

    artifactoryPublish {
        dependsOn jar
    }
}

gradlePlugin {
    plugins {
        ProzessPipelineGradlePlugin {
            id = 'de.seitenbau.ozghub.prozesspipeline'
            implementationClass = 'de.seitenbau.ozghub.prozesspipeline.ProzessPipelineGradlePlugin'
        }
    }
}

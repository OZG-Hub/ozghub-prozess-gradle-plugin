import com.github.spotbugs.snom.Effort
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'jacoco'
    id 'checkstyle'

    // https://plugins.gradle.org/plugin/com.github.spotbugs
    id 'com.github.spotbugs' version '6.4.5'

    // https://plugins.gradle.org/plugin/com.jfrog.artifactory
    id 'com.jfrog.artifactory' version '6.0.4'

    // https://plugins.gradle.org/plugin/com.gradle.plugin-publish
    // com.gradle.plugin-publish auto-applies `java-gradle-plugin` and `maven-publish` plugins
    id 'com.gradle.plugin-publish' version '2.0.0'

    // https://plugins.gradle.org/plugin/org.owasp.dependencycheck
    id 'org.owasp.dependencycheck' version '12.1.9'

    // https://plugins.gradle.org/plugin/com.github.ben-manes.versions
    id 'com.github.ben-manes.versions' version '0.53.0'
}

group = 'de.seitenbau.ozghub.prozessdeployment'

version = project.hasProperty('pluginVersion') ? pluginVersion : '99.0.0-SNAPSHOT'
println 'Plugin-Version ist  : ' + version

repositories {
    mavenLocal()
    maven {
        url = 'https://maven2.seitenbau.net/artifactory/all'
    }
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation 'commons-io:commons-io:2.21.0'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.25.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.25.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'

    // Zum serialisieren/deserialisieren von LocalDate nötig
    // kann entfernt werden wenn jackson das nativ unterstützt
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.1'

    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.8'

    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:6.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.1'

    // https://github.com/spotbugs/spotbugs-gradle-plugin#spotbugs-version-mapping
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.14.0'

    // https://projectlombok.org/setup/gradle
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    // Process sources using:
    // * `PluginProcessor` to generate `Log4j2Plugins.dat`
    // * `GraalVmProcessor` to generate a GraalVM reachability metadata file
    testAnnotationProcessor('org.apache.logging.log4j:log4j-core:2.25.2')
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    compileTestJava.options.compilerArgs = ["-Alog4j.graalvm.groupId=${project.group}", "-Alog4j.graalvm.artifactId=${project.name}"]
    javadoc.options.encoding = 'UTF-8'

    // ignore missing javadoc comments
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')
}

test {
    useJUnitPlatform()
    maxHeapSize = '1G'
    finalizedBy jacocoTestReport
    systemProperty 'file.encoding', 'UTF-8'
    testLogging {
        events TestLogEvent.FAILED
        exceptionFormat = 'full'
    }
}

jacocoTestReport {
    dependsOn test
}

// https://github.com/spotbugs/spotbugs-gradle-plugin#configure-the-spotbugstask
spotbugsMain {
    ignoreFailures = true
    effort = Effort.MAX
    setExcludeFilter(file('src/main/plugin-config/spotbugs-exclude.xml'))
}

// https://github.com/spotbugs/spotbugs-gradle-plugin/issues/391
spotbugsTest.enabled = false

// https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.CheckstyleExtension.html
checkstyle {
    // https://checkstyle.sourceforge.io/releasenotes.html
    checkstyleTest.enabled = false
}

if ('true' != System.getenv('CI')) {
    //##########  Lokaler Build  ##########
    getLogger().info('Lokaler Build')
    publishing {
        repositories {
            mavenLocal()
        }
    }
} else {
    //##########  Build auf CI  ##########
    getLogger().info('CI Build')
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    artifactory {
        publish {
            contextUrl = System.getenv('JF_REPO_URL')
            publishBuildInfo = false
            repository {
                repoKey = System.getenv('JF_REPO_KEY')
                username = System.getenv('JF_RW_USER')
                password = System.getenv('JF_RW_PASSWORD')
                ivy {
                    mavenCompatible = true
                }
            }
            defaults {
                publications('mavenJava')
            }
        }
    }

    jar {
        archiveBaseName = 'ozghub-prozessdeployment-gradle-plugin'
    }

    artifactoryPublish {
        dependsOn jar
    }
}

// Use java-gradle-plugin to generate plugin descriptors and override individual plugin properties.
// The names of the blocks inside 'plugins' need to be unique, they will identify the plugins being configured.
// 'id', 'implementationClass' and 'displayName' are mandatory.
// 'description' is optional and will override whatever description has been specified at bundle level, but
// in the end a description must be set for each plugin at some level.
// See https://plugins.gradle.org/docs/publish-plugin
// See https://docs.gradle.org/current/userguide/java_gradle_plugin.html
gradlePlugin {
    website = 'https://www.seitenbau.com/'
    vcsUrl = 'https://github.com/OZG-Hub/ozghub-prozess-gradle-plugin'
    plugins {
        OzghubProzessGradlePlugin {
            // You can ignore: Warning: Cannot assign 'String' to 'T'
            // See https://stackoverflow.com/questions/61983733/cannot-assign-string-to-t
            description = 'Tools zum automatischen Deployment von Prozessen im OZG-Hub'
            id = 'de.seitenbau.ozghub.prozessdeployment'
            implementationClass = 'de.seitenbau.ozghub.prozessdeployment.ProzessDeploymentGradlePlugin'
            displayName = 'Gradle Plugin zum Deployen von Prozessen und Formularen im OZG-Hub'
            tags.set(['serviceportal', 'deployment', 'prozesse', 'ozg'])
        }
    }
}

dependencyCheck {
    suppressionFile = 'config/dependency-suppression.xml'
    formats = ['HTML', 'JSON']
    // Disable use of the https://ossindex.sonatype.org/ as they use rate limiting which causes errors
    analyzers.ossIndexEnabled = false
    nvd.datafeedUrl = 'https://dependency-track.sbw.imbw.dev.seitenbau.net/mirror/nvd/nvdcve-2.0-modified.json.gz'
}

// https://github.com/ben-manes/gradle-versions-plugin?tab=readme-ov-file#rejectversionsif-and-componentselection
def isNonStable = { String version ->
    def regex = /^[0-9.]+(-jre)?$/
    return !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin?tab=readme-ov-file#rejectversionsif-and-componentselection
tasks.named('dependencyUpdates').configure {
    // https://github.com/ben-manes/gradle-versions-plugin?tab=readme-ov-file#gradle-release-channel
    dependencyUpdates.gradleReleaseChannel = 'current' // Don't suggest Gradle-RC Versions

    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}